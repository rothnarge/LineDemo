   10 REM SETUP BUFFERS
   20 MAXX=1280:MAXY=1024
   30 DIM B1(500):DIM B2(500)
   40 DIM B3(500):DIM B4(500)
   50 REM ASK USER
   60 MODE 1
   70 CLS
   80 PRINT"LINE SPACING";:INPUT S
   90 PRINT"TAIL LENGTH";:INPUT L
  100 PRINT"MULTIPLIER";:INPUT MULT
  110 IF MULT<1 THEN MULT=1
  120 REM SETUP INIT VAR
  130 D1=0:D2=1:D3=0:D4=1:REM DIRECTIONS
  140 A1=0:A2=0
  150 A3=50*MULT:A4=100*MULT
  160 C=0:REM BLACK
  170 CC=0:REM COLOR COUNTER
  180 P=0:REM POINTER TO CURRENT LINE
  190 E=1:REM E FOR POINTER TO LINE TO ERASE
  200 MODE 1
  210 CLS
  220 GOSUB 350
  230 GCOL 0,C
  240 PLOT 4,A1,A2
  250 PLOT 5,A3,A4
  260 B1(P)=A1:B2(P)=A2:B3(P)=A3:B4(P)=A4
  270 P=P+1:IF P=L THEN P=0
  280 GCOL 0,0
  290 PLOT 4,B1(E),B2(E)
  300 PLOT 5,B3(E),B4(E)
  310 E=E+1:IF E=L THEN E=0
  320 CC=CC+1:IF CC<>5 THEN GOTO 220
  330 CC=0:C=C+1:IF C=16 THEN C=1
  340 GOTO 220
  350 IF D1=0 THEN IF A1<=S THEN D1=1:A1=1:GOTO 390
  360 IF D1=0 THEN A1=A1-S:GOTO 390
  370 IF A1+S>=MAXX THEN D1=0:A1=MAXX-1:GOTO 390
  380 A1=A1+S
  390 IF D2=0 THEN IF A2<=S THEN D2=1:A2=1:GOTO 430
  400 IF D2=0 THEN A2=A2-S:GOTO 430
  410 IF A2+S>=MAXY THEN D2=0:A2=MAXY-1:GOTO 430
  420 A2=A2+S
  430 IF D3=0 THEN IF A3<=S THEN D3=1:A3=1:GOTO 470
  440 IF D3=0 THEN A3=A3-S:GOTO 470
  450 IF A3+S>=MAXX THEN D3=0:A3=MAXX-1:GOTO 470
  460 A3=A3+S
  470 IF D4=0 THEN IF A4<=S THEN D4=1:A4=1:RETURN
  480 IF D4=0 THEN A4=A4-S:RETURN
  490 IF A4+S>=MAXY THEN D4=0:A4=MAXY-1:RETURN
  500 A4=A4+S
  510 RETURN
